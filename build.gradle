import io.github.gradlenexus.publishplugin.InitializeNexusStagingRepository

plugins {
    id 'groovy'
    id 'io.github.gradle-nexus.publish-plugin'
}

version = projectVersion
ext.set('grailsVersion', libs.versions.grails.get())
ext.set('isSnapshot', projectVersion.endsWith('-SNAPSHOT'))
ext.set('isReleaseVersion', !isSnapshot)

if(isReleaseVersion) {
    nexusPublishing {
        String nexusUser = System.getenv("SONATYPE_USERNAME")
        String nexusPass = System.getenv("SONATYPE_PASSWORD")
        String nexusStagingProfileId = System.getenv("SONATYPE_STAGING_PROFILE_ID")
        repositories {
            sonatype {
                nexusUrl = uri('https://s01.oss.sonatype.org/service/local/')
                username = nexusUser
                password = nexusPass
                stagingProfileId = nexusStagingProfileId
            }
        }
    }
}


subprojects {
    apply plugin: 'groovy'

    version = rootProject.version
    repositories {
        mavenLocal() // Used by Groovy Joint Workflow
        mavenCentral()
        maven { url = 'https://repo.grails.org/grails/core' }
        if (libs.versions.groovy.get().endsWith('-SNAPSHOT')) {
            maven {
                name = 'JFrog Groovy snapshot repo'
                url = 'https://groovy.jfrog.io/artifactory/libs-snapshot-local/'
            }
        }
    }

    dependencies {
        implementation platform(libs.grails.bom)
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

//do not generate extra load on Nexus with new staging repository if signing fails
tasks.withType(InitializeNexusStagingRepository).configureEach {
    shouldRunAfter tasks.withType(Sign)
}