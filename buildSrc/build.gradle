plugins {
    id 'groovy-gradle-plugin'
    id 'maven-publish'
}

repositories {
    gradlePluginPortal()
    mavenCentral()
    maven { url = 'https://repo.grails.org/grails/core' }
}

configurations.configureEach { exclude group: 'org.apache.groovy' }

dependencies {
    // Needs to be implementation as classes are referenced in build scripts
    implementation buildsrcLibs.grails.docs
    implementation buildsrcLibs.nexus.publish.gradle.plugin

    runtimeOnly buildsrcLibs.grails.gradle.plugin
    runtimeOnly buildsrcLibs.grails.views.gradle.plugin
    runtimeOnly buildsrcLibs.groovydoc.gradle.plugin
}

def groovyVersion = project.rootProject
        .extensions
        .getByType(VersionCatalogsExtension.class)
        .named("libs")
        .findVersion("groovy")
        .get()
        .displayName

configurations.configureEach {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if ((details.requested.group == 'org.codehaus.groovy' || details.requested.group == 'org.apache.groovy') && details.requested.name != 'groovy-bom') {
            details.useTarget(group: 'org.apache.groovy', name: details.requested.name, version: groovyVersion)
            details.because "The dependency coordinates are changed in Apache Groovy 4, plus ensure version"
        }
    }
}