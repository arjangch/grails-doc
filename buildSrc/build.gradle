plugins {
    id 'groovy'
}

repositories {
    mavenLocal()
    maven { url = "https://repo.grails.org/grails/core" }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    configureEach {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if ((details.requested.group == 'org.codehaus.groovy' || details.requested.group == 'org.apache.groovy') && details.requested.name != 'groovy-bom') {
                details.useTarget(group: 'org.apache.groovy', name: details.requested.name, version: "$groovyVersion")
                details.because "The dependency coordinates are changed in Apache Groovy 4, plus ensure version"
            }
        }
    }
}

dependencies {
    implementation "org.grails:grails-docs:${grailsDocsVersion}"
    implementation "org.apache.groovy:groovy:$groovyVersion"
    implementation "org.grails:grails-gdoc-engine:1.0.1"
    implementation "org.grails:gdoc-to-asciidoc:1.0.1"
}
