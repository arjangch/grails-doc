plugins {
    id 'groovy-gradle-plugin'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    gradlePluginPortal()
    mavenCentral()
    maven { url = 'https://repo.grails.org/grails/core' }
}

dependencies {

    // Needs to be implementation as classes are referenced in build scripts
    implementation(buildsrcLibs.grails.docs) {
        exclude group: 'org.apache.groovy', module: 'groovy'
        exclude group: 'org.apache.groovy', module: 'groovy-ant'
    }
    implementation buildsrcLibs.nexus.publish.gradle.plugin

    runtimeOnly buildsrcLibs.grails.gradle.plugin
    runtimeOnly buildsrcLibs.grails.views.gradle.plugin
    runtimeOnly buildsrcLibs.groovydoc.gradle.plugin
}

configurations.configureEach {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        // This is a workaround for dependencies exposing a different version of Groovy
        // than the one used by Gradle. This causes issues with the Groovy compiler.
        if (details.requested.group == 'org.apache.groovy' || details.requested.group == 'org.codehaus.groovy') {
            details.useTarget(group: "org.codehaus.groovy", name: details.requested.name, version: GroovySystem.version)
            details.because "Use Groovy 3.0.x library shipped with Gradle"
        }
    }
}