plugins {
    id "com.gradle.enterprise" version '3.16.1'
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '1.12.1'
}

gradleEnterprise {
    server = 'https://ge.grails.org'
    buildScan {
        publishAlwaysIf(System.getenv('CI') == 'true')
        publishIfAuthenticated()
        uploadInBackground = System.getenv('CI') == null
        capture {
            taskInputFiles = true
        }
    }
}

buildCache {
    local { enabled = System.getenv('CI') != 'true' }
    remote(HttpBuildCache) {
        push = System.getenv('CI') == 'true'
        enabled = true
        url = 'https://ge.grails.org/cache/'
        credentials {
            username = System.getenv('GRADLE_ENTERPRISE_BUILD_CACHE_NODE_USER')
            password = System.getenv('GRADLE_ENTERPRISE_BUILD_CACHE_NODE_KEY')
        }
    }
}

rootProject.name = 'grails-async'

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            def groovyVersion = System.getenv('GROOVY_VERSION')
            if (groovyVersion) {
                logger.warn "Using custom version $groovyVersion of Groovy due to GROOVY_VERSION being set."
                version('groovy', groovyVersion)
            }
        }
    }
}

include 'docs'
include 'grails-async-core'
include 'grails-async-gpars'
include 'grails-async-rxjava'
include 'grails-async-rxjava2'
include 'grails-events-core'
include 'grails-events-compat'
include 'grails-events-transform'
include 'grails-events-gpars'
include 'grails-events-spring'
include 'grails-events-rxjava'
include 'grails-events-rxjava2'
include 'grails-plugin-async'
include 'grails-plugin-events'

file('examples').eachDir { dir ->
    include "examples-$dir.name"
    project(":examples-$dir.name").projectDir = dir
}